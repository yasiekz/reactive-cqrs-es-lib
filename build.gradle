plugins {
    id 'java'
}

apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group 'io.github.yasiekz'
archivesBaseName = "reactive-cqrs-es-lib"
version '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.springframework', name: 'spring-context', version: '5.1.7.RELEASE'
    compile group: 'io.projectreactor', name: 'reactor-core', version: '3.2.9.RELEASE'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
    testCompile group: 'io.projectreactor', name: 'reactor-test', version: '3.2.9.RELEASE'
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Reactive CQRS ES lib'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Library to handle reactive CQRS commands and events'
                url 'https://github.com/yasiekz/reactive-cqrs-es-lib'

                scm {
                    connection 'scm:git:git://github.com/yasiekz/reactive-cqrs-es-lib'
                    developerConnection 'scm:git:git@github.com:yasiekz/reactive-cqrs-es-lib.git'
                    url 'https://github.com/yasiekz/reactive-cqrs-es-lib'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'yasiekz'
                        name 'Jan Zieba'
                        email 'yasiekz@gmail.com'
                    }
                }
            }
        }
    }
}

